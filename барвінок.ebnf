(*
    Опис формату, який тут використовується: https://en.wikipedia.org/wiki/Extended_Backus–Naur_form
    Є лише одна модифікація формату: ідентифікатори тепер підтримають українську мову

    Пріоритет операторів:
        1. +х -х
        2. * / % \
        3. + -
        4. менше менше= більше більше= == !=
        5. не
        6. та
        7. або
*)

програма = інструкції, ? КІНЕЦЬ ФАЙЛУ ?;

інструкції = інструкція, {інструкція};
інструкція = інструкція якщо | інструкція поки | інструкція завершити | інструкція продовжити | інструкція функція | інструкція повернути | вираз;
вираз = вираз виклику | вираз присвоєння;

вираз присвоєння = ліва сторона, оператор присвоєння, права сторона;
вираз виклику = (вираз виклику | ідентифікатор), "(", [аргументи], ")";
аргументи = права сторона, {",", права сторона};

ліва сторона = ідентифікатор;
права сторона = пріоритет оператора 7;

пріоритет оператора 7 =
    | пріоритет оператора 7 "або" пріоритет оператора 6
    | пріоритет оператора 6

пріоритет оператора 6 =
    | пріоритет оператора 6 "та" пріоритет оператора 5
    | пріоритет оператора 5

пріоритет оператора 5 =
    | "не" пріоритет оператора 5
    | пріоритет оператора 4

пріоритет оператора 4 =
    | пріоритет оператора 4 "менше" пріоритет оператора 3
    | пріоритет оператора 4 "менше=" пріоритет оператора 3
    | пріоритет оператора 4 "більше" пріоритет оператора 3
    | пріоритет оператора 4 "більше=" пріоритет оператора 3
    | пріоритет оператора 4 "==" пріоритет оператора 3
    | пріоритет оператора 4 "!=" пріоритет оператора 3
    | пріоритет оператора 3

пріоритет оператора 3 =
    | пріоритет оператора 3 "+" пріоритет оператора 2
    | пріоритет оператора 3 "-" пріоритет оператора 2
    | пріоритет оператора 2

пріоритет оператора 2 =
    | пріоритет оператора 2 "*" пріоритет оператора 1
    | пріоритет оператора 2 "/" пріоритет оператора 1
    | пріоритет оператора 2 "%" пріоритет оператора 1
    | пріоритет оператора 2 "\" пріоритет оператора 1
    | пріоритет оператора 1

пріоритет оператора 1 =
    | "+" пріоритет оператора 1
    | "-" пріоритет оператора 1
    | простий вираз

простий вираз = ідентифікатор | вираз виклику | вираз літерал | вираз у дужках;
вираз літерал = булеве | ціле число | дійсне число | нічого;
вираз у дужках = "(" , права сторона, ")";

оператор = "+" | "-" | "*" | "/" | "\" | "%" | "та" | "або" | "==" | "!=" | "більше" | "більше=" | "менше" | "менше=";
унарний оператор  = "+" | "-" | "не";
оператор присвоєння = "=" | "+=" | "-=" | "*=" | "/=" | "\=" | "%=";

інструкція якщо = "якщо", права сторона, інструкції, [інструкція інакше | інструкція або якщо], кінець блоку;
інструкція або якщо = "або якщо", права сторона, інструкції, [інструкція або якщо];
інструкція інакше = "інакше", інструкції;
інструкція поки = "поки", права сторона, інструкції, кінець блоку;
інструкція завершити = "завершити";
інструкція продовжити = "продовжити";
інструкція функція = "функція", ідентифікатор, "(", [параметри функції], ")", інструкції, кінець блоку;
параметри функції = ідентифікатор, {",", ідентифікатор};
інструкція повернути = "повернути", (";" | [права сторона]);

ідентифікатор = (символ | "_"), {символ | цифра | "_"};
кінець блоку = "кінець";
булеве = "істина" | "хиба";
нічого = "нич";
ціле число = ["-" | "+"], цифра без нуля, {цифра};
дійсне число = ["-" | "+"], (({цифра}, ".", цифра, {цифра}) | (цифра, {цифра}, ".", {цифра}));

символ =
      "А" | "а" | "Б" | "б" | "В" | "в" | "Г" | "г" | "Ґ" | "ґ" | "Д" | "д" | "Е" | "е" | "Є" | "є" | "Ж" | "ж"
    | "З" | "з" | "И" | "и" | "І" | "і" | "Ї" | "ї" | "Й" | "й" | "К" | "к" | "Л" | "л" | "М" | "м" | "Н" | "н"
    | "О" | "о" | "П" | "п" | "Р" | "р" | "С" | "с" | "Т" | "т" | "У" | "у" | "Ф" | "ф" | "Х" | "х" | "Ц" | "ц"
    | "Ч" | "ч" | "Ш" | "ш" | "Щ" | "щ" | "Ь" | "ь" | "Ю" | "ю" | "Я" | "я";

цифра без нуля = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";
цифра = "0" | цифра без нуля;
