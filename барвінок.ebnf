(*
    Опис формату, який тут використовується: https://en.wikipedia.org/wiki/Extended_Backus–Naur_form
    Є лише одна модифікація формату: ідентифікатори тепер підтримають українську мову
*)

програма = інструкції;

інструкції = інструкція, {інструкція};
інструкція = інструкція якщо | інструкція поки | інструкція завершити | інструкція продовжити | інструкція функція | інструкція повернути | вираз;
вираз = вираз присвоєння | вираз виклику;

вираз присвоєння = ліва сторона, оператор присвоєння, права сторона;
вираз виклику = ідентифікатор, "(", [аргументи], ")";
аргументи = права сторона, {",", права сторона};

ліва сторона = ідентифікатор;
права сторона = бінарний вираз | унарний вираз | простий вираз;

бінарний вираз = (права сторона | простий вираз), оператор, права сторона;
унарний вираз = унарний оператор, права сторона;
простий вираз = ідентифікатор | вираз виклику | вираз літерал | вираз у дужках;
вираз літерал = булеве | ціле число | дійсне число | нічого;
вираз у дужках = "(" , права сторона, ")";

оператор = "+" | "-" | "*" | "/" | "\" | "%" | "та" | "або" | "==" | "!=" | "більше" | "більше=" | "менше" | "менше=";
унарний оператор  = "+" | "-" | "не";
оператор присвоєння = "=" | "+=" | "-=" | "*=" | "/=" | "\=" | "%=";

інструкція якщо = "якщо", права сторона, інструкції, [інструкція або якщо | інструкція інакше], кінець блоку;
інструкція або якщо = "або якщо", права сторона, інструкції, [інструкція або якщо];
інструкція інакше = "інакше", інструкції;
інструкція поки = "поки", права сторона, інструкції, кінець блоку;
інструкція завершити = "завершити";
інструкція продовжити = "продовжити";
інструкція функція = "функція", ідентифікатор, "(", [параметри функції], ")", інструкції, кінець блоку;
параметри функції = ідентифікатор, {",", ідентифікатор};
інструкція повернути = "повернути", [права сторона];

ідентифікатор = (символ | "_"), {символ | цифра | "_"};
кінець блоку = "кінець";
булеве = "істина" | "хиба";
нічого = "нич";
ціле число = ["-" | "+"], цифра без нуля, {цифра};
дійсне число = ["-" | "+"], (({цифра}, ".", цифра, {цифра}) | (цифра, {цифра}, ".", {цифра}));

символ =
      "А" | "а" | "Б" | "б" | "В" | "в" | "Г" | "г" | "Ґ" | "ґ" | "Д" | "д" | "Е" | "е" | "Є" | "є" | "Ж" | "ж"
    | "З" | "з" | "И" | "и" | "І" | "і" | "Ї" | "ї" | "Й" | "й" | "К" | "к" | "Л" | "л" | "М" | "м" | "Н" | "н"
    | "О" | "о" | "П" | "п" | "Р" | "р" | "С" | "с" | "Т" | "т" | "У" | "у" | "Ф" | "ф" | "Х" | "х" | "Ц" | "ц"
    | "Ч" | "ч" | "Ш" | "ш" | "Щ" | "щ" | "Ь" | "ь" | "Ю" | "ю" | "Я" | "я";

цифра без нуля = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";
цифра = "0" | цифра без нуля;
